apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    compile project(path: ":hmac-auth-common")

    compile 'com.beust:jcommander:1.82'

    testCompile 'org.testng:testng:7.5'
    testCompile 'org.hamcrest:hamcrest-library:2.2'
    testCompile 'org.mockito:mockito-core:4.5.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hmac-auth-cli'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'hmac-auth-cli'
                description = 'HMAC CLI features.'
                url = 'http://github.com/otto-de/hmac-auth'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/hmac-auth.git'
                    developerConnection = 'scm:git@github.com:otto-de/hmac-auth.git'
                    url = 'http://github.com/otto-de/hmac-auth'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenJava
}

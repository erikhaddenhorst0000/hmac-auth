
dependencies {
    implementation("jakarta.servlet:jakarta.servlet-api:6.0.0") { ext.provided = true }
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'

    implementation 'com.sun.jersey.contribs:jersey-apache-client4:1.19.4'

    implementation 'org.aspectj:aspectjweaver:1.9.19'

    implementation 'commons-codec:commons-codec:1.15'
    implementation 'com.google.guava:guava:31.1-jre'

    implementation 'org.slf4j:slf4j-api:1.7.36'

    implementation 'com.sun.jersey:jersey-client:1.19.4'
    implementation 'com.sun.jersey:jersey-core:1.19.4'

    implementation project(path: ":hmac-auth-common")

    testImplementation 'org.testng:testng:7.5'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.mockito:mockito-core:4.10.0'
    testImplementation 'org.springframework:spring-test:6.0.8'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hmac-auth-jersey-client'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'hmac-auth-jersey-client'
                description = 'Jersey client for HMAC authentication.'
                url = 'http://github.com/otto-de/hmac-auth'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/hmac-auth.git'
                    developerConnection = 'scm:git@github.com:otto-de/hmac-auth.git'
                    url = 'http://github.com/otto-de/hmac-auth'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenJava
}

dependencies {
    implementation 'org.glassfish.jersey.core:jersey-client:3.1.2'

    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    
    implementation project(path: ":hmac-auth-common")
    
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'commons-io:commons-io:2.11.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.mockito:mockito-core:4.10.0'
    // jersey test framework container with servlet support
    testImplementation 'org.glassfish.jersey.test-framework:jersey-test-framework-core:3.1.2'
    testImplementation 'org.glassfish.jersey.containers:jersey-container-grizzly2-servlet:3.1.2'
    testImplementation 'org.glassfish.jersey.inject:jersey-hk2:3.1.2'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

test {
    useJUnit()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hmac-auth-jersey2-client'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'hmac-auth-jersey2-client'
                description = 'Jersey 2 client for HMAC authentication.'
                url = 'http://github.com/otto-de/hmac-auth'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/hmac-auth.git'
                    developerConnection = 'scm:git@github.com:otto-de/hmac-auth.git'
                    url = 'http://github.com/otto-de/hmac-auth'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
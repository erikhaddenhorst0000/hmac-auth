apply plugin: "application"

mainClassName = "de.otto.hmac.proxy.ProxyServer"

sourceCompatibility = 17
targetCompatibility = 17


dependencies {
    implementation project(path: ":hmac-auth-jersey-client")

    implementation 'com.sun.jersey:jersey-server:1.19.4'
    implementation 'com.sun.jersey:jersey-grizzly2:1.19.4'
    implementation 'com.sun.jersey.contribs:jersey-apache-client4:1.19.4'
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    implementation 'com.sun.xml.bind:jaxb-impl:4.0.1'
    implementation 'com.sun.xml.bind:jaxb-core:4.0.1'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'com.beust:jcommander:1.82'

    implementation 'org.slf4j:slf4j-log4j12:1.7.36'

    testImplementation 'org.testng:testng:7.5'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.mockito:mockito-core:4.10.0'
}

artifacts {
    archives distZip
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hmac-auth-proxy'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'hmac-auth-proxy'
                description = 'HMAC authentication proxy.'
                url = 'http://github.com/otto-de/hmac-auth'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/hmac-auth.git'
                    developerConnection = 'scm:git@github.com:otto-de/hmac-auth.git'
                    url = 'http://github.com/otto-de/hmac-auth'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenJava
}